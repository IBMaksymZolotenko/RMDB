--	-------------------------------------------------------------------------------------------------------------------
--	
--		2019/10/23, Золотенко М.
--	
--		Сфери діяльності, що зутстрічаються у аплікаційних даних
--		
CREATE TABLE [Scoring].[ActivityField]
	(
		[ID]							INT				NOT NULL	IDENTITY(1,1),

		[POSITION]						NVARCHAR(200)	NOT NULL,
		[FIELD_OF_ACTIVITY]				NVARCHAR(200)	NOT NULL,
		[DESCRIPTION]					NVARCHAR(MAX),
		[GROUP_FIELD_OF_ACTIVITY]		NVARCHAR(200),
		[GROUP_POSITION]				NVARCHAR(200),

		[CREATED]						DATETIME2(7)	NOT NULL	DEFAULT SYSDATETIME(),
		[CREATOR]						NVARCHAR(100)	NOT NULL	DEFAULT USER,
		[CHANGED]						DATETIME2(7)		NULL,
		[CHANGER]						NVARCHAR(100)		NULL,

		CONSTRAINT PK_SCORING_ACTIVITYFIELD PRIMARY KEY (ID),
		CONSTRAINT UQ_SCORING_ACTIVITYFIELD__POSITION__FIELD_OF_ACTIVITY UNIQUE (POSITION, FIELD_OF_ACTIVITY),
	);
GO

CREATE TRIGGER [Scoring].[trg_ActivityField]
	ON [Scoring].[ActivityField]
AFTER UPDATE, DELETE
AS

	SET NOCOUNT ON;

	INSERT	[Scoring].[ActivityFieldHistory]
		(
			[SCORING_ACTIVITY_FIELD_ID], [POSITION],[FIELD_OF_ACTIVITY],[DESCRIPTION],[GROUP_FIELD_OF_ACTIVITY],[GROUP_POSITION],[CREATED],[CREATOR],[CHANGED],[CHANGER]
		)
	SELECT	[ID],[POSITION],[FIELD_OF_ACTIVITY],[DESCRIPTION],[GROUP_FIELD_OF_ACTIVITY],[GROUP_POSITION],[CREATED],[CREATOR],[CHANGED],[CHANGER]
	FROM	DELETED T1
	
	EXCEPT
	
	SELECT	[ID],[POSITION],[FIELD_OF_ACTIVITY],[DESCRIPTION],[GROUP_FIELD_OF_ACTIVITY],[GROUP_POSITION],[CREATED],[CREATOR],[CHANGED],[CHANGER]
	FROM	INSERTED

	UPDATE	T2
	SET		T2.CHANGED = SYSDATETIME(),
			T2.CHANGER = USER
	FROM	(
				SELECT	[ID],[POSITION],[FIELD_OF_ACTIVITY],[DESCRIPTION],[GROUP_FIELD_OF_ACTIVITY],[GROUP_POSITION],[CREATED],[CREATOR],[CHANGED],[CHANGER]
				FROM	DELETED T1
				EXCEPT
				SELECT	[ID],[POSITION],[FIELD_OF_ACTIVITY],[DESCRIPTION],[GROUP_FIELD_OF_ACTIVITY],[GROUP_POSITION],[CREATED],[CREATOR],[CHANGED],[CHANGER]
				FROM	INSERTED
			) V1
	INNER JOIN [Scoring].[ActivityField] T2 ON T2.ID = V1.ID
GO